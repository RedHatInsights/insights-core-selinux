## <summary>policy for insights_core</summary>

########################################
## <summary>
##     Allow explicit transition to insights_core_t domain.
## </summary>
## <param name="domain">
## <summary>
##     Domain allowed to transition.
## </summary>
## </param>
#
interface(`insights_core_domtrans',`
    gen_require(`
        type insights_core_t;
    ')
    allow $1 insights_core_t: process transition;
    allow insights_core_t $1:fd use;
    allow insights_core_t $1:fifo_file rw_file_perms;
    allow insights_core_t $1:process sigchld;
    allow insights_core_t $1:dir search_dir_perms;
')

########################################
## <summary>
##     Write to an insights_core unnamed pipe.
## </summary>
## <param name="domain">
## <summary>
##     Domain allowed access.
## </summary>
## </param>
#
interface(`insights_core_write_pipes',`
    gen_require(`
        type insights_core_t;
    ')
    allow $1 insights_core_t:fifo_file write_fifo_file_perms;
')


########################################
## <summary>
##     Read insights_client lib files.
## </summary>
## <param name="domain">
## <summary>
##     Domain allowed access.
## </summary>
## </param>
#
interface(`insights_core_read_lib_files',`
    gen_require(`
        type insights_core_var_lib_t;
    ')
    files_search_var_lib($1)
    read_files_pattern($1, insights_core_var_lib_t, insights_core_var_lib_t)
    allow $1 insights_core_var_lib_t:file map;
')


########################################
## <summary>
##    Start insights_client service
## </summary>
## <param name="domain">
##    <summary>
##    Domain allowed access
##    </summary>
## </param>
#
ifndef(`insights_client_service_start',`
    interface(`insights_client_service_start',`
        gen_require(`
            type insights_client_unit_file_t;
        ')

        allow $1 insights_client_unit_file_t:service { status start };
    ')
')

########################################
## <summary>
##    Stop insights_client service
## </summary>
## <param name="domain">
##    <summary>
##    Domain allowed access
##    </summary>
## </param>
#
ifndef(`insights_client_service_stop',`
    interface(`insights_client_service_stop',`
        gen_require(`
            type insights_client_unit_file_t;
        ')

        allow $1 insights_client_unit_file_t:service { status stop };
    ')
')


########################################
## <summary>
##     Execute container in the container domain.
## </summary>
## <param name="domain">
## <summary>
##     Domain allowed to transition.
## </summary>
## </param>
#
ifndef(`container_runtime_domtrans',`
    interface(`container_runtime_domtrans',`
        gen_require(`
            type container_runtime_t, container_runtime_exec_t;
            type container_runtime_tmpfs_t;
        ')
        corecmd_search_bin($1)
        domtrans_pattern($1, container_runtime_exec_t, container_runtime_t)
        allow container_runtime_t $1:fifo_file setattr;
    ')
')

########################################
## <summary>
##     Allow the domain to use the io_uring API via an inherited file
##     descriptor.
## </summary>
## <param name="domain">
## <summary>
##     Domain allowed access.
## </summary>
## </param>
#
ifndef(`kernel_io_uring_use_inherited',`
    interface(`kernel_io_uring_use_inherited',`
        gen_require(`
            type io_uring_t;
        ')
        allow $1 io_uring_t:anon_inode { getattr read write map };
    ')
')

########################################
## <summary>
##     Allow the domain to use the io_uring API.
## </summary>
## <param name="domain">
## <summary>
##     Domain allowed access.
## </summary>
## </param>
#
ifndef(`kernel_io_uring_use',`
    interface(`kernel_io_uring_use',`
        gen_require(`
            type io_uring_t;
        ')
        kernel_io_uring_use_inherited($1)
        allow $1 io_uring_t:anon_inode create;
    ')
')

########################################
## <summary>
##     Execute sap in sap unconfined domain.
## </summary>
## <param name="domain">
## <summary>
##     Domain allowed to transition.
## </summary>
## </param>
#
ifndef(`sap_unconfined_domtrans',`
    interface(`sap_unconfined_domtrans',`
        gen_require(`
            type sap_unconfined_t, sap_exec_t;
        ')
        corecmd_search_bin($1)
        domtrans_pattern($1, sap_exec_t, sap_unconfined_t)
    ')
')
